cmake_minimum_required(VERSION 3.3)
cmake_policy(SET CMP0057 NEW)

project(SCREAM CXX Fortran)

# Print the sha of the last commit (useful to double check which version was tested on CDash)
EXECUTE_PROCESS (COMMAND git rev-parse HEAD
                 WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
                 OUTPUT_VARIABLE LAST_GIT_COMMIT_SHA
                 OUTPUT_STRIP_TRAILING_WHITESPACE)
SET (LAST_GIT_COMMIT_SHA ${LAST_GIT_COMMIT_SHA} CACHE STRING "The sha of the last git commit.")
MESSAGE (STATUS "The sha of the last commit is ${LAST_GIT_COMMIT_SHA}")

# Set the scream base and src directory, to be used across subfolders
SET (SCREAM_BASE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
SET (SCREAM_SRC_DIR  ${CMAKE_CURRENT_SOURCE_DIR}/src)

# Add the ./cmake folder to cmake path
SET (CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_CURRENT_SOURCE_DIR}/cmake)

# Shortcut function, to print a variable
function (print_var var)
  message ("${var}: ${${var}}")
endfunction ()

enable_testing()
include(CTest)

# Scream configuration options
set(SCREAM_DOUBLE_PRECISION FALSE CACHE LOGICAL "Set to double precision (default True)")
set(SCREAM_ENABLE_FPE FALSE CACHE LOGICAL "Enable floating point error exception")
set(SCREAM_TEST_DATA_DIR ${CMAKE_BINARY_DIR}/data CACHE FILEPATH "Location of data files generated by tests")

set(CATCH_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/extern/catch2/include)

# Find Kokkos, initialize some compiler/linke variable based on what we find in it,
# then proceed with setting the rest of the compiler(s) flags
include(Kokkos)
set (CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -cpp")
SET (CMAKE_CXX_FLAGS ${KOKKOS_CXXFLAGS_STR})

# Set compiler-specific flags
include(SetCompilerFlags)

set (SCREAM_LINK_FLAGS ${KOKKOS_LDFLAGS_STR})
set (SCREAM_INCLUDE_DIRS ${Kokkos_INCLUDE_DIR} ${SCREAM_SRC_DIR})
set (SCREAM_LIBRARY_DIRS ${Kokkos_LIBRARY_DIR})
set (SCREAM_LIBRARIES ${KOKKOS_LIBS})


if (${SCREAM_DOUBLE_PRECISION})
  add_definitions(-DSCREAM_DOUBLE_PRECISION)
  if ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Intel")
    set(CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -real-size 64 ")
  else()
    set(CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -fdefault-real-8 ")
  endif()
endif()

if (${SCREAM_ENABLE_FPE})
  add_definitions(-DSCREAM_FPE)
endif()

print_var(SCREAM_COMPILE_FLAGS)
print_var(SCREAM_INCLUDE_DIRS)
print_var(SCREAM_LINK_FLAGS)
print_var(SCREAM_LIBRARY_DIRS)
print_var(SCREAM_LIBRARIES)

set (SCREAM_F90_MODULES ${CMAKE_BINARY_DIR}/modules)
set (SCREAM_DATA_DIR ${CMAKE_SOURCE_DIR}/data)

file(MAKE_DIRECTORY ${SCREAM_TEST_DATA_DIR})

add_subdirectory(src)
add_subdirectory(tests)

add_custom_target(baseline DEPENDS p3_baseline)
